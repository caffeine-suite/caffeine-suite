import &StandardImport, &BabelBridge, &SemanticTree, &Lib

->
  @rule
    doubleQuote:            /"/
    singleQuote:            /'/

    interpolationStart:     /\#\{/
    interpolationEnd:       /\}/

    dqStringMiddle:         /// ( [^"\\#]    | \\. | \#(?!\{) )* ///
    sqStringMiddle:         /// ( [^'\\#]    | \\. | \#(?!\{) )* ///
    blockStringMiddle:      /// ( [^\\#]     | \\.     | \#(?!\{) )* ///

  @rule
    stringLiteral:
      {}
        pattern: '/""/ tripple:/"/? &/ +[^ \\n]| *\\n/ stringBlock'
        getStn: ~>
          ret = @stringBlock.getStn()
          ret.compactNewLines() unless @tripple
          ret.trimRight?()
          ret

      {}
        pattern: "/''/ tripple:/'/? &/ +[^ \\n]| *\\n/ unparsedBlock"
        getStn: ~>
          ret = StringStn value: @unparsedBlock.toString()
          ret.compactNewLines() unless @tripple
          ret

      ## word-strings
        :unquoted strings
        : is omitted
        reserved: '::' for a future word-string-block
        reserved: backslashes are currently excluded until I decide
          if they should be escaped or
          if we should support escapes
          Could be interesting: :foo\sbar could == "foo bar"
      {}
        pattern: /:(?!:)[^\\\n\s,]+/
        getStn: ~> StringStn value: @toString().slice 1

      ## hashtag-strings
        #hashtag and color (#fff) strings
        The # is preserved
      {}
        pattern: /#[$\w\u007f-\uffff]+/
        getStn: ~> StringStn value: @toString()

      # number-with-unit strings
      {}
        pattern:
          ///
          [-+]?
          (?!00)
          [0-9]*
          \.?
          [0-9]+
          ([eE][-+]?[0-9]+)?

          # followed by one or more identifier characters
          [$\w\u007f-\uffff]+
          ///
        getStn: ~> StringStn value: @toString()

    stringBlock: Extensions.IndentBlocks.getPropsToSubparseToEolAndBlock rule: "stringBlockBody"

  @rule
    stringLiteral:
      "bracketStart:doubleQuote mid:dqStringMiddle interpolation:dqStringInterpolation? doubleQuote"
      "bracketStart:singleQuote mid:sqStringMiddle interpolation:sqStringInterpolation? singleQuote"

    stringBlockBody: "/[ \\n]*/ mid:blockStringMiddle interpolation:blockStringInterpolation?"

    {}
      getStnChildren: (appendTo = [])~>
        appendTo.push StringStn value: @mid.toString() if @mid.matchLength > 0
        @interpolation?.getStnChildren appendTo
        appendTo

      getStn: ~>
        ret = if @interpolation
          InterpolatedStringStn @getStnChildren()
        else
          StringStn value: @mid.toString()

        if @bracketStart
          ret.compactNewLines true, true
        ret

  @rule
    interpolation:
      "interpolationStart expression interpolationEnd"
      "interpolationStart expression:rValueBlock _end? interpolationEnd"

  @rule
    dqStringInterpolation:    "interpolation mid:dqStringMiddle interpolationContinues:dqStringInterpolation?"
    sqStringInterpolation:    "interpolation mid:sqStringMiddle interpolationContinues:sqStringInterpolation?"
    blockStringInterpolation: "interpolation mid:blockStringMiddle interpolationContinues:blockStringInterpolation?"
    {}
      getStnChildren: (appendTo = [])~>
        appendTo.push @interpolation.expression.getStn()
        appendTo.push StringStn value: @mid.toString() if @mid.matchLength > 0
        @interpolationContinues?.getStnChildren appendTo
        appendTo
