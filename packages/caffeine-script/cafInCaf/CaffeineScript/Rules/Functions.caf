import &StandardImport, &BabelBridge

getPropertySetters = (node, list = []) ~>
  if node
    if prop = node.shouldSetProperty?()
      list.push prop
    else
      each match in node.matches
        getPropertySetters match, list
  list

~>
  @rule
    functionDefinition:
      pattern: "args:argsDefinition? _arrow_ body:functionDefinitionBodyBlock?"
    {}
      stnFactory: :FunctionDefinitionStn
      stnProps: ~>
        bound: switch @_arrow_.text.match(/(=>|~>|->)/)[0]
          when "=>" then true
          when "~>" then false
          when "->" then "auto"
          else throw new Error

  @rule
    functionDefinitionBodyBlock: Extensions.IndentBlocks.getPropsToSubparseToEolAndBlock()

  @rule
    argsDefinition:
      "openParen_ argDefList? _closeParen"
    {}
      stnFactory: :FunctionDefinitionArgsStn

  @rule
    argDefList:
      "argDef _comma_ argDefList"
      "argDef _ argDefList"
      "argDef"

    argDef:
      pattern: "at:/@/? identifier argIdentifierExtension?"
      stnFactory: "FunctionDefinitionArgStn"
      stnProps: ~> rest: !!@argIdentifierExtension?.etc, assignThisProperty: !!@at

    argIdentifierExtension:
      :defaultValue
      :etc

    defaultValue:
      pattern: "_equals_ expression"

  @rule
    etc: "'...'"

  @rule superFunctionInvocation:
    "openParen_ valueList? _closeParen"
    "_? complexExpression"
    "_? valueListBlock"

  @rule
    functionInvocation:
      "existanceTest:questionMark? openParen_ values:valueList? _closeParen"
      "existanceTest:questionMark? _? values:complexExpression"
      "existanceTest:questionMark? _? values:valueListBlock"
    {}
      stnFactory: "FunctionInvocationStn"
      stnExtension: true
      stnProps: ~> existanceTest: !!@existanceTest
      stnChildren: ~> @values?.getStn()
