import &StandardImport, &Lib

class StatementsStn extends &BaseStn

  toJs: ->
    @getChildrenStatementsJsArray().join "; "

  toFunctionBodyJs: (returnAction = true)->
    @toFunctionBodyJsArray returnAction
    .join "; "

  toFunctionBodyJsArray: (returnAction = true)->
    @getChildrenStatementsJsArray returnAction

  getChildrenStatementsJsArray: (returnAction, generateStatements = true)->
    if returnAction == true
      returnAction = "return"

    array c, i in lines = @children
      if returnAction && i == lines.length - 1
        if !c.jsExpressionUsesReturn
          "#{returnAction} #{c.toJsExpression()}"
        else c.toJs generateReturnStatement: true

      else
        if generateStatements
          statement = c.toJsStatement()
          if statement.match /^function/
            @applyRequiredParens statement
          else
            statement
        else
          c.toJsExpression returnValueIsIgnored: true

  toJsExpression: ->
    @toJsParenExpression()

  toJsParenExpression: ->
    switch @children.length
    when 0 then "undefined"
    when 1 then @children[0].toJsParenExpression()
    else
      @applyRequiredParens
        @getChildrenStatementsJsArray "", false
        .join ", "

  toJsExpressionWithParens: -> @toJsParenExpression()

