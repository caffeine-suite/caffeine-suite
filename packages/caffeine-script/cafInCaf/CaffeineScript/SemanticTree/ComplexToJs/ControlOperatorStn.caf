import &StandardImport
StnRegistry = &StnRegistry

class ControlOperatorStn extends &BaseStn

  constructor: (props, children) ->
    super
    @operand    = props.operand
    @joiner     = props.joiner
    if @labeledChildren.expression
      @expression = @labeledChildren.expression
      @body       = @labeledChildren.body       || StnRegistry.UndefinedStn()
      @elseBody   = @labeledChildren.elseBody
    else
      @expression = children[0]
      @body       = children[1] || StnRegistry.UndefinedStn()
      @elseBody   = children[2]
    @validate()

  validate: ->
    switch @operand
    when "while", "until"
      throw new Error "else not expected after #{@operand}" if @elseBody
      throw new Error "then not expected after #{@operand}" if @joiner == "then"
    when "if", "unless"
      throw new Error "do not expected after #{@operand}" if @joiner == "do"
    else
      throw new Error "INTERNAL: invalid control-operator: #{formattedInspect @operand}"

  toJs: (options = {})->
    {expression, returnValueIsIgnored} = options
    jsExpression = @expression.toJsExpression()
    {operand} = @

    operand = switch operand
      when "until", "unless"
        jsExpression = "!#{@applyParens jsExpression}"
        if operand == "until" then "while" else "if"
      else operand

    if expression
      if operand == "while"
        if returnValueIsIgnored
          "
          (() => {while
          #{@applyRequiredParens jsExpression}
          {#{@body.toFunctionBodyJs false};};})()
          "
        else
          tempVarIdentifier = @scope.uniqueIdentifier
          "
          (() => {while
          #{@applyRequiredParens jsExpression}
          {#{@body.toFunctionBodyJs "#{tempVarIdentifier} ="};};
          return #{tempVarIdentifier}})()
          "
      else
        "
        #{@applyParens jsExpression} ?
        #{@body.toJsParenExpression()} :
        #{@elseBody?.toJsParenExpression() || 'undefined'}
        "
    else
      "
      #{operand}
      #{@applyRequiredParens jsExpression}
      {#{@body.toJs()};}#{if @elseBody then " else {#{@elseBody?.toJs()};}" else ''}
      "

  toJsParenExpression: ->
    @applyRequiredParens @toJs expression: true

  toJsExpression: (returnValueIsIgnored) ->
    @toJs expression: true, returnValueIsIgnored: returnValueIsIgnored
