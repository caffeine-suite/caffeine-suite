import &StandardImport

class SuperStn extends &BaseStn
  ##
    props:
      calledInConstructor: t/f

  constructor: (props, @args) ->
    super
    # collapse implicit arrays into parents
    if @args.length == 1 && @args[0].props.implicitArray
      @args = @args[0].children

  needsParens: false

  postTransform: ->
    throw new Error "super must be used inside an object-literal value" unless propValue = @findParent "ObjectPropValue"
    # TODO: we should validate that propName has a legal value string

    methodName = propValue.labeledChildren.propName.props.value
    [__, classMethod, methodName] = m if m = methodName.match /^(@)(.*)/
    new @class merge(@props, methodName: methodName, classMethod: !!classMethod), @transformChildren()

  toJs: ->
    {args} = @

    if @props.calledInConstructor
      args = if @props.passArguments
        ["...arguments"]
      else
        array a in args with a.toJsExpression()

      "super(#{args.join ', '})"

    else
      objectPropValue = @findParent "ObjectPropValue"
      getSuperInput = if klass = @findParent "Class"
        className = klass.labeledChildren.className.toJs()

        superObject = if @props.classMethod
          klass.props.classSuperHandle
        else
          klass.props.instanceSuperHandle

        method = if @props.passArguments
          args = "arguments"
          "apply"
        else
          args = array a in args with a.toJsExpression()
          "call"

        "#{superObject}.#{@props.methodName}.#{method}#{@applyRequiredParens ['this'].concat(args).join ', '}"

      else
        throw new Error "super not used in class"
