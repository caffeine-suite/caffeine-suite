# WARNING: jsPretty BREAKS the javascript output. The problem is jsPretty removes () around ({a} = b)
# which breaks NODE v6 (but not chrome or safari).
# See my NODE issue report: https://github.com/nodejs/node/issues/11480

import &ArtFoundation
&Path
&Fs

class RequireStn extends &BaseStn

  updateScope: (@scope) ->
    @scope.addIdentifierAssigned @rawIdentifier, "require('#{@requireString}')"
    super

  @getter
    normalizedIdentifier: -> upperCamelCase @rawIdentifier
    npmIdentifier: -> dashCase @rawIdentifier
    rawIdentifier: -> @children[0].props.identifier
    requireString: ->
      if relativeFile = @findFileInPackage()
        relativeFile
      else
        @npmIdentifier

  findFileInPackage: (options = @parser.options) ->
    {sourceFile, sourceFiles, sourceRoot} = options
    if (sourceFile ||= sourceFiles?[0]) && sourceRoot

      {normalizedIdentifier} = @

      directory = sourceDir = Path.resolve Path.dirname sourceFile
      sourceRoot = Path.resolve sourceRoot

      found = null
      shouldContinue = true
      while shouldContinue
        found = find name from files = Fs.readdirSync directory
          [baseName] = name.split '.'
          normalizedName = upperCamelCase baseName
          if normalizedName == normalizedIdentifier
            relative = Path.relative sourceDir, directory
            relative = Path.join relative, baseName
            relative = "./#{relative}" unless relative.match /^\./
            relative
        if found || directory == sourceRoot
          shouldContinue = false
        directory = Path.dirname directory
      found

  toJs: ->
    @rawIdentifier